#include <OneButton.h>

OneButton buttonUp(A1, true);
OneButton buttonDown(A2, true);

const int BUFFER_SIZE = 12;
const int BUFFER_SIZE_UP = 13;

int batteryLevel = 0;
int power = 0;
int engineTemp = 0; 
double speed = 0;

int currentGear = 0;
int previousGear = 0;

byte buf[BUFFER_SIZE_UP];
byte buf_up[BUFFER_SIZE_UP] = {13, 1, 5, 86, 9, 154, 40, 202, 4, 20, 1, 50, 14}; // sample packet generated by the web calculator
 
void setup() {
  //Setup serial connection
  Serial.begin(9600);
  Serial1.begin(9600);

  buttonUp.attachClick(increaseGear);
  buttonDown.attachClick(decreaseGear);
  buttonDown.attachLongPressStart(walkMode);
  buttonDown.attachLongPressStop(stopWalkMode);
}
 
void loop() {
  if (Serial1.available() >= BUFFER_SIZE) { // check if there are enough available bytes to read
    Serial1.readBytes(buf, BUFFER_SIZE); // read bytes to the buf array
  }
  processPacket(buf);
  Serial.print("Speed: ");
  Serial.print(speed);
  Serial.print(" Battery level: ");
  Serial.print(batteryLevel);
  Serial.print(" Engine temp: ");
  Serial.print(engineTemp);
  Serial.print(" Power: ");
  Serial.print(power);
  Serial.print(" Current gear: ");
  Serial.println(currentGear);

  buf_up[1] = currentGear;
  buf_up[5] = calculateCRC(buf_up);
  Serial1.write(buf_up, BUFFER_SIZE_UP);


  buttonUp.tick();
  buttonDown.tick();
  delay(10);
}

void increaseGear() {
  if (currentGear < 5) {
    currentGear++;
  }
} 
void decreaseGear(){       
  if (currentGear > 0) {
    currentGear--;
  }
}
void walkMode() {
  previousGear = currentGear;
  currentGear = 6;
}
void stopWalkMode() {
  currentGear = previousGear;
}
int calculateCRC(byte buf_up[]) {
  int crc = 0;
  for (int i = 0; i < BUFFER_SIZE_UP; i++) {
    if (i != 5) {
      crc ^= buf_up[i];
    }
  }
  crc ^= 3;
  return crc;
}
void processPacket(byte buf[]) {
  speed =  60000 / (buf[3] * 256 + buf[4]) * 0.1885 * 0.66;
  batteryLevel = buf[1];
  power = buf[8] * 13;
  engineTemp = uint8_t(buf[9]) + 15;
}